module(
    name = "rules_wasm",
    version = "2.0.1",
)

bazel_dep(name = "buildifier_prebuilt", version = "8.2.0.2", dev_dependency = True, repo_name = "buildtools")

bazel_dep(name = "bazel_skylib", version = "1.8.0")
bazel_dep(name = "gazelle", version = "0.44.0")
bazel_dep(name = "package_metadata", version = "0.0.3")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.1.2")
bazel_dep(name = "rules_go", version = "0.55.1", repo_name = "io_bazel_rules_go")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_rust", version = "0.62.0")
bazel_dep(name = "rules_shell", version = "0.5.0")

# The example module (which contains basic tests)
# is itself referenced by tests in the main module,
# which verify that some example tests are running properly.
bazel_dep(name = "example", dev_dependency = True)
local_path_override(
    module_name = "example",
    path = "example",
)

# Supported execution platforms for pre-built binaries.
# TODO: Figure out how to dedupe this list with `private.bzl`.
execution_platforms = [
    "aarch64-linux",
    "aarch64-macos",
    "x86_64-linux",
    "x86_64-macos",
]

# https://github.com/WebAssembly/binaryen/releases
binaryen_version = "123"

binaryen_integrity = {
    "aarch64-linux": "sha384-gRHcVA3ixZu857DlrGkamJbWE73ec4mrq+e3SxF/EjV7wI7WRzHlSPEN3adzCb11",
    "aarch64-macos": "sha384-BNHNaw4V4C7VD9VW6+77+lgzns0LjZBEtMb/HpoH67xmZf7muEwXWgGUzDFMPKbk",
    "x86_64-linux": "sha384-ChUj3KQRLPlMFzI4go6A43jQMp1Db7RiV3EH1aHO9MxPI3rXxIzl8ieHBlpIAF30",
    "x86_64-macos": "sha384-+jVPAGdgH1mZhGKbIQl3a5NFXNA952mWanfW07ysUUC7uqYMACz7D0dGIJ9TGAYf",
}

# https://pkg.go.dev/go.bytecodealliance.org/cm?tab=versions
go_cm_version = "0.3.0"

go_cm_integrity = "h1:VhV+4vjZPUGCozCg9+up+FNL3YU6XR+XKghk7kQ0vFc="

# https://github.com/tinygo-org/tinygo/releases
tinygo_version = "0.38.0"

tinygo_integrity = {
    "aarch64-linux": "sha384-Z6zdIRLp7PKFtCXueMNX0bmy+gE3TUZ3g9zyoLBK7IQwollx3QMIhWz4RAVnGXlQ",
    "aarch64-macos": "sha384-pn1hq6ZWk7MwdNZE3I3poYQOx5LNO2oLECcrJ1sCog3IQaZt+SncQTkbrvx+rji0",
    "x86_64-linux": "sha384-5nAHSMs8zqYoycl0AVLAr0l2awkzkac0YbNOC35GbXPhthsdTwZlPNpMyGroXNtu",
    "x86_64-macos": "sha384-Y6PulpPkg4kIrMa6TaVqWqDETIlmLYPrlwegxOlM/dnvAOnGfN+PYGuOnFA+6y1p",
}

# https://github.com/bytecodealliance/wac/releases
wac_version = "0.7.0"

wac_integrity = {
    "aarch64-linux": "sha384-iZWru00Qm9z7BxweEsojSlj7ubdH9iFCyPAmWsODNhRkEKfPnQ3ki/ysUYZJDvb0",
    "aarch64-macos": "sha384-6njLwvToPOfKEfU6769GrlZxo25lbsj219z8F8xv1aycxIOXyidabYstA7SrJIUd",
    "x86_64-linux": "sha384-w9Ea0H2kdg+bPd5nJN7gFCJkX7E8EACD0D9208cSa9hpqWP+0kwEeyH7b2GiJ9hD",
    "x86_64-macos": "sha384-7HgXDtsXuWKl1R5kAG9jwMaBCPkcarXCbHDLTbvufASm1RaO0o/1+Y11SWxFgO6d",
}

# ATTOW TinyGo only supports version `0.2.0`:
# https://github.com/WebAssembly/WASI/releases
wasi_version = "0.2.0"

wasi_integrity = "sha384-k5+dIIcSf9AtW/dpJ4wHglf9GCs0OZw5I90Lkd74RZ/X8PMv0Cx9WKFPsTFPCV9H"

# https://github.com/WebAssembly/wasi-sdk/releases
wasi_sdk_version = "25"

wasi_sdk_integrity = {
    "aarch64-linux": "sha384-5bnm2bABYAmVF0KeEgz3SAbo4qKRQJkByW9eQZ6KIqV+BF2KiZEEjKI4+ELdJPqH",
    "aarch64-macos": "sha384-T6tTh2QN9bfrKptJ80vGCNoyvRbcXHEkzP805V9THXkSFsQD/fwHYkOWD7VOf61f",
    "x86_64-linux": "sha384-2hliAV3H1pxMMROU9BarleLEH0dng/XdvfpSsGJKbvRvyy98K59WD8fN5RrvaCML",
    "x86_64-macos": "sha384-zVs1lhkofsVL6pWDsKlFEPjWeMxmH19tQX3a4jhfO1XbLlga7Rw/pYQvfReiY7AU",
}

# https://github.com/bytecodealliance/wasmtime/releases
wasmtime_version = "34.0.1"

wasmtime_integrity = {
    "aarch64-linux": "sha384-J7DeHNnCfuI8h+7MR0ZeAwXhoypCdK1HRX1gHaj9LZgNDSo/GvqEjOzdquOFhPKE",
    "aarch64-macos": "sha384-MyslrefvqDKffzX5XkOsQQELVjmjuqGwWw/pdDovQ/PBq66Cw3mAmeflaaLz8sG8",
    "x86_64-linux": "sha384-gJDLMte87YZ0TCRTX9qIkijhJ1kRYQBnKJ9/kDfDG/AuMaovCdkQtEovsJynR0RV",
    "x86_64-macos": "sha384-dkjJod98jIDTF7N001PUvlPNMRqwxe5W6SrIcWW+wYssxKx1FYynkOmQlyW9PB9r",
}

# https://github.com/bytecodealliance/wasm-tools/releases
wasm_tools_version = "1.235.0"

wasm_tools_integrity = {
    "aarch64-linux": "sha384-mbE74PDs3d/z2ZzIlklYSVMCp8DU2c179Vujm+kU109WztbS557SFCl7BUTTGK5d",
    "aarch64-macos": "sha384-cI9D2hXa+0nwx9xQZme+hTmpICHH+jZviNWIkv4nBtC4G/PIRV0aTkTPCHw1bzP4",
    "x86_64-linux": "sha384-UbGbw0AfmsGKsRV9AmWCP2An3s+/qvmSwR6baKRqjMEhOEKsbvSEw+AaWohn9aMc",
    "x86_64-macos": "sha384-BBiakYt4GLeubvPorHrFaI/x7Agyii/DzM7QJ5j2/7ijDmcf4SEgOTNy3ISVdPaM",
}

# https://github.com/bytecodealliance/wit-bindgen/releases
wit_bindgen_version = "0.43.0"

wit_bindgen_integrity = {
    "aarch64-linux": "sha384-8unEVH/DGI263VG89LGmlL4KASNu+Kr/8/wyKDysSdtgGohHJ5op5AVYAAs3JbMK",
    "aarch64-macos": "sha384-Rjf+yg6iJYPusBxxsqmIXPWZUStT64Q4gQy/z+V9N3rzvf8lS6Cq5FvQktIRKEvZ",
    "x86_64-linux": "sha384-3fPD7PxgIybcmXQ/mU+Y4fnQanreegAyQXQ6qqOoTLlY8p6g5pjzEF9u8k2Grot9",
    "x86_64-macos": "sha384-C0OYKQzfOCiaqRz0IPPFxrGlk/pDXCWgYLq4vIHk4D1TaGaitfv11r+9fSaKWCUA",
}

# https://github.com/bytecodealliance/go-modules/releases
wit_bindgen_go_version = "0.7.0"

wit_bindgen_go_integrity = {
    "aarch64-linux": "sha384-cbNymuUTUpvyb0jVrN+Fy97NgFYmGyJQ+Ml7QyMa98G4KMKAi/Cbp/nRY/xGhvZX",
    "aarch64-macos": "sha384-Yn43/bbLp4HKX2oz85ESWZOJgsa0La30Vda+SabuowxtS4604R+9h0ks1n1J0xGV",
    "x86_64-linux": "sha384-a5R9jT7HdEHxManvsn8qhyP0Qma/kFousmmPVy4j+QHLat0eDh6aTfyXq1W6KyUd",
    "x86_64-macos": "sha384-WmFNc+tWLOCe8zYuYBcQ/99ffOensY/FN1XmijQpVIWSBqz/KSIsPgDitVx2LJJF",
}

# Rust setup: wit-bindgen-generated Rust files depend on the wit-bindgen crate.
crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.spec(
    package = "wit-bindgen",
    version = wit_bindgen_version,
)
crate.from_specs(
    name = "rules-wasm-crates",
    # Hardcode all supported Rust toolchains to make Cargo splicing more efficient.
    # https://github.com/bazelbuild/rules_rust/discussions/2259
    supported_platform_triples = [
        "aarch64-unknown-linux-gnu",
        "aarch64-apple-darwin",
        # TODO: Updgrade to wasip2 when there's an available platform
        #       (`@platforms//os:wasi` is only supported by wasip1).
        "wasm32-wasip1",
        "x86_64-unknown-linux-gnu",
        "x86_64-apple-darwin",
    ],
)
use_repo(crate, "rules-wasm-crates")

# Manually download prebuilt binaries for various dependencies:

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

[http_archive(
    name = "binaryen-" + exe_platform,
    build_file_content = """
alias(
    name = "wasm-opt",
    actual = "binaryen-version_{version}/bin/wasm-opt",
    visibility = ["//visibility:public"],
)
""".format(version = binaryen_version),
    integrity = binaryen_integrity[exe_platform],
    url = "https://github.com/WebAssembly/binaryen/releases/download/version_{version}/binaryen-version_{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "aarch64-linux",
            "aarch64-macos": "arm64-macos",
            "x86_64-linux": "x86_64-linux",
            "x86_64-macos": "x86_64-macos",
        }[exe_platform],
        version = binaryen_version,
    ),
) for exe_platform in execution_platforms]

wac_platforms = {
    "aarch64-linux": "aarch64-unknown-linux-musl",
    "aarch64-macos": "aarch64-apple-darwin",
    "x86_64-linux": "x86_64-unknown-linux-musl",
    "x86_64-macos": "x86_64-apple-darwin",
}

[http_file(
    name = "wac-" + exe_platform,
    executable = True,
    integrity = wac_integrity[exe_platform],
    url = "https://github.com/bytecodealliance/wac/releases/download/v{version}/wac-cli-{platform}".format(
        # WAC uses non-canonical platform strings in the URLs:
        platform = wac_platforms[exe_platform],
        version = wac_version,
    ),
) for exe_platform in execution_platforms]

http_archive(
    name = "wasi",
    build_file_content = """
load(":wasm.bzl", "wasi_packages")

[filegroup(
    name = package,
    srcs = glob(["WASI-{version}/preview2/" + package + "/*.wit"]),
    visibility = ["//visibility:public"],
) for package in wasi_packages.keys()]
""".format(version = wasi_version),
    integrity = wasi_integrity,
    # Ideally there would be something like `remote_file_urls` for local files
    # so we can eliminate this redundant patch file
    # and instead just copy `wasm.bzl` into the remote repo:
    # https://github.com/bazelbuild/bazel/issues/24499.
    patches = ["//wasm:wasi.patch"],
    url = "https://github.com/WebAssembly/WASI/archive/refs/tags/v{version}.tar.gz".format(version = wasi_version),
)

wasi_sdk_platforms = {
    "aarch64-linux": "arm64-linux",
    "aarch64-macos": "arm64-macos",
    "x86_64-linux": "x86_64-linux",
    "x86_64-macos": "x86_64-macos",
}

[http_archive(
    name = "wasi-sdk-" + exe_platform,
    # Note: these are symlinks within the HTTP archive:
    # - clang -> clang-19
    # - ar -> llvm-ar
    # - nm -> llvm-nm
    # - objdump -> llvm-objdump
    # - strip -> llvm-objcopy
    build_file_content = """
load("@bazel_skylib//rules/directory:directory.bzl", "directory")
load("@bazel_skylib//rules/directory:subdirectory.bzl", "subdirectory")

package(default_visibility=["//visibility:public"])

alias(name = "clang", actual = "wasi-sdk-{version}.0-{platform}/bin/clang")
alias(name = "wasm-component-ld", actual = "wasi-sdk-{version}.0-{platform}/bin/wasm-component-ld")
alias(name = "ar", actual = "wasi-sdk-{version}.0-{platform}/bin/ar")
alias(name = "nm", actual = "wasi-sdk-{version}.0-{platform}/bin/nm")
alias(name = "objdump", actual = "wasi-sdk-{version}.0-{platform}/bin/objdump")
alias(name = "strip", actual = "wasi-sdk-{version}.0-{platform}/bin/strip")

directory(
    name = "lib",
    srcs = glob(["wasi-sdk-{version}.0-{platform}/lib/**"]),
)
subdirectory(
    name = "include",
    parent = ":lib",
    path = "clang/19/include",
)
""".format(
        platform = wasi_sdk_platforms[exe_platform],
        version = wasi_sdk_version,
    ),
    integrity = wasi_sdk_integrity[exe_platform],
    url = "https://github.com/WebAssembly/wasi-sdk/releases/download/wasi-sdk-{version}/wasi-sdk-{version}.0-{platform}.tar.gz".format(
        platform = wasi_sdk_platforms[exe_platform],
        version = wasi_sdk_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wasmtime-" + exe_platform,
    build_file_content = """
alias(
    name = "wasmtime",
    actual = "wasmtime-v{version}-{platform}/wasmtime",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wasmtime_version,
    ),
    integrity = wasmtime_integrity[exe_platform],
    url = "https://github.com/bytecodealliance/wasmtime/releases/download/v{version}/wasmtime-v{version}-{platform}.tar.xz".format(
        platform = exe_platform,
        version = wasmtime_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wasm-tools-" + exe_platform,
    build_file_content = """
alias(
    name = "wasm-tools",
    actual = "wasm-tools-{version}-{platform}/wasm-tools",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wasm_tools_version,
    ),
    integrity = wasm_tools_integrity[exe_platform],
    url = "https://github.com/bytecodealliance/wasm-tools/releases/download/v{version}/wasm-tools-{version}-{platform}.tar.gz".format(
        platform = exe_platform,
        version = wasm_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wit-bindgen-" + exe_platform,
    build_file_content = """
alias(
    name = "wit-bindgen",
    actual = "wit-bindgen-{version}-{platform}/wit-bindgen",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wit_bindgen_version,
    ),
    integrity = wit_bindgen_integrity[exe_platform],
    url = "https://github.com/bytecodealliance/wit-bindgen/releases/download/v{version}/wit-bindgen-{version}-{platform}.tar.gz".format(
        platform = exe_platform,
        version = wit_bindgen_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "tinygo-" + exe_platform,
    build_file_content = """
alias(
    name = "tinygo",
    actual = "tinygo/bin/tinygo",
    visibility = ["//visibility:public"],
)
""",
    integrity = tinygo_integrity[exe_platform],
    url = "https://github.com/tinygo-org/tinygo/releases/download/v{version}/tinygo{version}.{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = tinygo_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wit-bindgen-go-" + exe_platform,
    build_file_content = """
exports_files(
    ["wit-bindgen-go"],
    visibility = ["//visibility:public"],
)
""",
    integrity = wit_bindgen_go_integrity[exe_platform],
    url = "https://github.com/bytecodealliance/go-modules/releases/download/v{version}/wit-bindgen-go-{platform}.tgz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = wit_bindgen_go_version,
    ),
) for exe_platform in execution_platforms]

# Needed to build Rust components.
http_file(
    name = "libwit-bindgen-cabi",
    integrity = "sha384-tYAxxKVIpYi9UUR9Bo+42ngHFjI9bHMfLxmhW08hZo9SYQVJNRGB9f/il4oS0Bv3",
    url = "https://github.com/bytecodealliance/wit-bindgen/raw/v{version}/crates/guest-rust/rt/src/libwit_bindgen_cabi.a".format(version = wit_bindgen_version),
)

# Temporary crutch until we can build against wasip2 directly.
http_file(
    name = "wasi-snapshot-preview1-reactor",
    integrity = "sha384-3Ci0S49o8mosFRAqbuUjBhwD+meEiLTlcjV28FkVki/cguWU/A36zHPmFNG6yHQc",
    url = "https://github.com/bytecodealliance/wasmtime/releases/download/v{version}/wasi_snapshot_preview1.reactor.wasm".format(version = wasmtime_version),
)

go_repository = use_repo_rule("@gazelle//:deps.bzl", "go_repository")

# The only dependency for generated Go bindings.
go_repository(
    name = "go-component-model-utility",
    importpath = "go.bytecodealliance.org/cm",
    # The patch expose the `go.mod` file
    # as well as the Gazelle-generated package info.
    patches = ["//go:cm.patch"],
    sum = go_cm_integrity,
    version = "v{}".format(go_cm_version),
)

register_toolchains("//:wasm32-wasi-toolchain")
