module(
    name = "rules_wasm",
    version = "2.0.1",
)

bazel_dep(name = "buildifier_prebuilt", version = "8.2.1", dev_dependency = True, repo_name = "buildtools")

bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "gazelle", version = "0.46.0")
bazel_dep(name = "package_metadata", version = "0.0.6")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "rules_go", version = "0.58.3", repo_name = "io_bazel_rules_go")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_rust", version = "0.67.0")
bazel_dep(name = "rules_shell", version = "0.6.1")

# The example module (which contains basic tests)
# is itself referenced by tests in the main module,
# which verify that some example tests are running properly.
bazel_dep(name = "example", dev_dependency = True)
local_path_override(
    module_name = "example",
    path = "example",
)

# Supported execution platforms for pre-built binaries.
# TODO: Figure out how to dedupe this list with `private.bzl`.
execution_platforms = [
    "aarch64-linux",
    "aarch64-macos",
    "x86_64-linux",
    "x86_64-macos",
]

# https://github.com/WebAssembly/binaryen/releases
binaryen_version = "124"

# https://pkg.go.dev/go.bytecodealliance.org/cm?tab=versions
go_cm_version = "0.3.0"

# https://github.com/tinygo-org/tinygo/releases
tinygo_version = "0.39.0"

# https://github.com/bytecodealliance/wac/releases
wac_version = "0.8.0"

# ATTOW TinyGo only supports version `0.2.0`:
# https://github.com/WebAssembly/WASI/releases
wasi_version = "0.2.0"

# https://github.com/WebAssembly/wasi-sdk/releases
wasi_sdk_version = "27"

# https://github.com/bytecodealliance/wasmtime/releases
wasmtime_version = "38.0.3"

# https://github.com/bytecodealliance/wasm-tools/releases
wasm_tools_version = "1.240.0"

# https://github.com/bytecodealliance/wit-bindgen/releases
wit_bindgen_version = "0.46.0"

# https://github.com/bytecodealliance/go-modules/releases
wit_bindgen_go_version = "0.7.0"

# Rust setup: wit-bindgen-generated Rust files depend on the wit-bindgen crate.
crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.spec(
    package = "wit-bindgen",
    version = wit_bindgen_version,
)
crate.from_specs(
    name = "rules-wasm-crates",
    # Hardcode all supported Rust toolchains to make Cargo splicing more efficient.
    # https://github.com/bazelbuild/rules_rust/discussions/2259
    supported_platform_triples = [
        "aarch64-unknown-linux-gnu",
        "aarch64-apple-darwin",
        "wasm32-wasip2",
        "x86_64-unknown-linux-gnu",
        "x86_64-apple-darwin",
    ],
)
use_repo(crate, "rules-wasm-crates")

# Manually download prebuilt binaries for various dependencies:

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

[http_archive(
    name = "binaryen-" + exe_platform,
    build_file_content = """
alias(
    name = "wasm-opt",
    actual = "binaryen-version_{version}/bin/wasm-opt",
    visibility = ["//visibility:public"],
)
""".format(version = binaryen_version),
    integrity = {
        "aarch64-linux": "sha384-zCF87XjVxy61cA6tMChEUEsNkikQMIMlNbNvBCz1+e5dBazWxcfsDGhImOxSpcjc",
        "aarch64-macos": "sha384-BSXU7J6EjAgZT14+6AfQHDGC4lxNEiHrJDClVquXO5tRmMAC9nuPfdjxgauyX4aK",
        "x86_64-linux": "sha384-Kkqn4LXPLP5BdaD49Rt6y3BIKKSzBEziEy6jQI6L8EjPPtYKOHayKjO5SsyWT0ON",
        "x86_64-macos": "sha384-HaS6V+gOz/aVPh/fK3gHV0XcasMRgmDz4hQa7RZcjWkhEBXVnbTBawI+Nex6cBe3",
    }[exe_platform],
    url = "https://github.com/WebAssembly/binaryen/releases/download/version_{version}/binaryen-version_{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "aarch64-linux",
            "aarch64-macos": "arm64-macos",
            "x86_64-linux": "x86_64-linux",
            "x86_64-macos": "x86_64-macos",
        }[exe_platform],
        version = binaryen_version,
    ),
) for exe_platform in execution_platforms]

wac_platforms = {
    "aarch64-linux": "aarch64-unknown-linux-musl",
    "aarch64-macos": "aarch64-apple-darwin",
    "x86_64-linux": "x86_64-unknown-linux-musl",
    "x86_64-macos": "x86_64-apple-darwin",
}

[http_file(
    name = "wac-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-SeWq2Uvh2N2pb9MEGzgndX6vRjPO8bcIT3FN1fJ6/2L13LD2SxGODRxj2HSbjxhZ",
        "aarch64-macos": "sha384-I7HqTHtAB8Za96CfJTHdkwxBT3rBTj8ofZMlr3Rczh+RV87cZDnAQdZfL4Ya+ggs",
        "x86_64-linux": "sha384-bw2BnPQV0b+fT2QkFqav7EnIzFk6DCbBsugl64+e5Th34/bz6KmN60eXTIhmZJ/G",
        "x86_64-macos": "sha384-+qfODImXDJshjAQs8hrMJZly1jfgnXRg8unzltMCHYo9OPyeA0PCJNcPMfnBGV+l",
    }[exe_platform],
    url = "https://github.com/bytecodealliance/wac/releases/download/v{version}/wac-cli-{platform}".format(
        # WAC uses non-canonical platform strings in the URLs:
        platform = wac_platforms[exe_platform],
        version = wac_version,
    ),
) for exe_platform in execution_platforms]

http_archive(
    name = "wasi",
    build_file_content = """
load(":wasm.bzl", "wasi_packages")

[filegroup(
    name = package,
    srcs = glob(["WASI-{version}/preview2/" + package + "/*.wit"]),
    visibility = ["//visibility:public"],
) for package in wasi_packages.keys()]
""".format(version = wasi_version),
    integrity = "sha384-k5+dIIcSf9AtW/dpJ4wHglf9GCs0OZw5I90Lkd74RZ/X8PMv0Cx9WKFPsTFPCV9H",
    # Ideally there would be something like `remote_file_urls` for local files
    # so we can eliminate this redundant patch file
    # and instead just copy `wasm.bzl` into the remote repo:
    # https://github.com/bazelbuild/bazel/issues/24499.
    patches = ["//wasm:wasi.patch"],
    url = "https://github.com/WebAssembly/WASI/archive/refs/tags/v{version}.tar.gz".format(version = wasi_version),
)

wasi_sdk_platforms = {
    "aarch64-linux": "arm64-linux",
    "aarch64-macos": "arm64-macos",
    "x86_64-linux": "x86_64-linux",
    "x86_64-macos": "x86_64-macos",
}

[http_archive(
    name = "wasi-sdk-" + exe_platform,
    # Note: these are symlinks within the HTTP archive:
    # - clang -> clang-19
    # - ar -> llvm-ar
    # - nm -> llvm-nm
    # - objdump -> llvm-objdump
    # - strip -> llvm-objcopy
    build_file_content = """
load("@bazel_skylib//rules/directory:directory.bzl", "directory")
load("@bazel_skylib//rules/directory:subdirectory.bzl", "subdirectory")

package(default_visibility=["//visibility:public"])

alias(name = "clang", actual = "wasi-sdk-{version}.0-{platform}/bin/clang")
alias(name = "wasm-component-ld", actual = "wasi-sdk-{version}.0-{platform}/bin/wasm-component-ld")
alias(name = "ar", actual = "wasi-sdk-{version}.0-{platform}/bin/ar")
alias(name = "nm", actual = "wasi-sdk-{version}.0-{platform}/bin/nm")
alias(name = "objdump", actual = "wasi-sdk-{version}.0-{platform}/bin/objdump")
alias(name = "strip", actual = "wasi-sdk-{version}.0-{platform}/bin/strip")

directory(
    name = "lib",
    srcs = glob(["wasi-sdk-{version}.0-{platform}/lib/**"]),
)
subdirectory(
    name = "include",
    parent = ":lib",
    path = "clang/19/include",
)
""".format(
        platform = wasi_sdk_platforms[exe_platform],
        version = wasi_sdk_version,
    ),
    integrity = {
        "aarch64-linux": "sha384-PmsbdCPgOr4iCcESHv52Qq+fchIajCzp0KjtSI+wVmdbITfmhz5Z/B6Zo1wsMbbf",
        "aarch64-macos": "sha384-9rkAAa2qHmae/e/W8Ae+2V6h/0HWrgVCgPaYoS4tjBqT7DT/GLPux2uCQM7V1UTH",
        "x86_64-linux": "sha384-co4l1I2SeWppYyCayaJEYyZvl5iNXfStSvS17GY/dYY4LqFxIW/YuOSn/ov8AbHL",
        "x86_64-macos": "sha384-/JCJLwJo6s67R61G8bb0jUrec6q383HZzecBJwoZ4SWgRTcFFxUXAviYUZlrvCMm",
    }[exe_platform],
    url = "https://github.com/WebAssembly/wasi-sdk/releases/download/wasi-sdk-{version}/wasi-sdk-{version}.0-{platform}.tar.gz".format(
        platform = wasi_sdk_platforms[exe_platform],
        version = wasi_sdk_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wasmtime-" + exe_platform,
    build_file_content = """
alias(
    name = "wasmtime",
    actual = "wasmtime-v{version}-{platform}/wasmtime",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wasmtime_version,
    ),
    integrity = {
        "aarch64-linux": "sha384-BcTk96h8SXt5KsWZEQF8o+fd5pjFw5w/mkZOOq6QA2g2pLxnaAAg/3wDEaebl/MZ",
        "aarch64-macos": "sha384-IZ43SDqg8hcXmVGoLMcltlPpxVZe8Cf05SzL6vOvEqnavDit9FjxEYWZtRB+kqhE",
        "x86_64-linux": "sha384-d4Jp9Kz33FVDPy3cF1OBJZ+tNOF1jAlkcHWNGjnBChFyZazkPlflBxgbVvNPZzk1",
        "x86_64-macos": "sha384-6aglu7GWtLATDl/nQQX+twxy8izzXWYv4z3+43vuodrKGNIJW6+5hQPA1NiGTLKm",
    }[exe_platform],
    url = "https://github.com/bytecodealliance/wasmtime/releases/download/v{version}/wasmtime-v{version}-{platform}.tar.xz".format(
        platform = exe_platform,
        version = wasmtime_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wasm-tools-" + exe_platform,
    build_file_content = """
alias(
    name = "wasm-tools",
    actual = "wasm-tools-{version}-{platform}/wasm-tools",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wasm_tools_version,
    ),
    integrity = {
        "aarch64-linux": "sha384-86NbIslFwLsTgSdNJFVmHP8O9TXW0ILIAPHvcTYQ0pBdlxlMgyr/Se60yaynCkCC",
        "aarch64-macos": "sha384-TTdAI6pzUpvFuNE7jHh6cfVVNoE+B+vQp02XPNlSEVKbV0kOqnZ9gQrTrdfOmvbx",
        "x86_64-linux": "sha384-Bg+YxFssBhR6JZB7k4JawjQE7QmJxXBuL5b0mR4jQQl5K6JOW8efj0UC8UxjsPjz",
        "x86_64-macos": "sha384-IUBtbT65llrRA99m8m+MCcyrhqFXCcnSmvfrljB6ntknawriQmB8Wf/8KNCDaIOS",
    }[exe_platform],
    url = "https://github.com/bytecodealliance/wasm-tools/releases/download/v{version}/wasm-tools-{version}-{platform}.tar.gz".format(
        platform = exe_platform,
        version = wasm_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wit-bindgen-" + exe_platform,
    build_file_content = """
alias(
    name = "wit-bindgen",
    actual = "wit-bindgen-{version}-{platform}/wit-bindgen",
    visibility = ["//visibility:public"],
)
""".format(
        platform = exe_platform,
        version = wit_bindgen_version,
    ),
    integrity = {
        "aarch64-linux": "sha384-9dEKp+dsT3Rx5ZkRQLE07hMZ9vHmhIuijG03RizJ2BOfR2Gf5PsH5KKqekKHW3jy",
        "aarch64-macos": "sha384-0nNKUcuiXibpCXyJSXnKjdlB24FwqG+E5xht8bbL4eE1ofMlX5PVllVj/lj38khI",
        "x86_64-linux": "sha384-08MgnOrAAZ9wHW4q+70ZMDHSfgiJKFMwCkgLX/s0etJWll2daFQmuKo6+LgfXgSC",
        "x86_64-macos": "sha384-UUd0pgj5/oe0XJ0G2Y0w3LZep9S76O+CyfqOjRjZBsUf165n8y32K5nOCasOps2J",
    }[exe_platform],
    url = "https://github.com/bytecodealliance/wit-bindgen/releases/download/v{version}/wit-bindgen-{version}-{platform}.tar.gz".format(
        platform = exe_platform,
        version = wit_bindgen_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "tinygo-" + exe_platform,
    build_file_content = """
alias(
    name = "tinygo",
    actual = "tinygo/bin/tinygo",
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-YzSti1CVRxYGVhn5ihWLgCwGiZnR9M6bSl2AklXxX4XUqQGqsJ8JiLK5AMc+3D54",
        "aarch64-macos": "sha384-YZywVFCfTar+yd6d7IlWW518PTJUi0n11FUfO8h6fXorFxMxvrL3sW5CDj10Ts6E",
        "x86_64-linux": "sha384-fv7Q2NWBsZT0YaxwaPsvx8ib/AvVYaS3GanOYZZdl8Koffb0kS/bTHSHVD0OOA7+",
        "x86_64-macos": "sha384-CPPi8Z0kBo+9zNlya5KUMsMata4czS9VOnkdjHH3iyuS8OxBvhP9nZiIVIkJEAVi",
    }[exe_platform],
    url = "https://github.com/tinygo-org/tinygo/releases/download/v{version}/tinygo{version}.{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = tinygo_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "wit-bindgen-go-" + exe_platform,
    build_file_content = """
exports_files(
    ["wit-bindgen-go"],
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-cbNymuUTUpvyb0jVrN+Fy97NgFYmGyJQ+Ml7QyMa98G4KMKAi/Cbp/nRY/xGhvZX",
        "aarch64-macos": "sha384-Yn43/bbLp4HKX2oz85ESWZOJgsa0La30Vda+SabuowxtS4604R+9h0ks1n1J0xGV",
        "x86_64-linux": "sha384-a5R9jT7HdEHxManvsn8qhyP0Qma/kFousmmPVy4j+QHLat0eDh6aTfyXq1W6KyUd",
        "x86_64-macos": "sha384-WmFNc+tWLOCe8zYuYBcQ/99ffOensY/FN1XmijQpVIWSBqz/KSIsPgDitVx2LJJF",
    }[exe_platform],
    url = "https://github.com/bytecodealliance/go-modules/releases/download/v{version}/wit-bindgen-go-{platform}.tgz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = wit_bindgen_go_version,
    ),
) for exe_platform in execution_platforms]

# Temporary crutch until we can build against wasip2 directly.
http_file(
    name = "wasi-snapshot-preview1-reactor",
    integrity = "sha384-bQWyruDsSVq78gXCUSECmMSdxae5maZyNZTrEXe49XQfmRQEO4WLoyIgEyTnJzr2",
    url = "https://github.com/bytecodealliance/wasmtime/releases/download/v{version}/wasi_snapshot_preview1.reactor.wasm".format(version = wasmtime_version),
)

go_repository = use_repo_rule("@gazelle//:deps.bzl", "go_repository")

# The only dependency for generated Go bindings.
go_repository(
    name = "go-component-model-utility",
    importpath = "go.bytecodealliance.org/cm",
    # The patch expose the `go.mod` file
    # as well as the Gazelle-generated package info.
    patches = ["//go:cm.patch"],
    sum = "h1:VhV+4vjZPUGCozCg9+up+FNL3YU6XR+XKghk7kQ0vFc=",
    version = "v{}".format(go_cm_version),
)

register_toolchains("//:wasm32-wasi-toolchain")
