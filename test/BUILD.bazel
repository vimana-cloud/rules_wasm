load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("//:private.bzl", "execution_platforms")

# List of platform-dependent binaries,
# where `{}` is a placeholder for the platform name.
binaries = [
    "@wasmtime-{}//:wasmtime",
    "@wasm-tools-{}//:wasm-tools",
    "@wit-bindgen-{}//:wit-bindgen",
    "@wac-{}//file",
]

# Download all remote files for all platforms
# to make sure the checksums are correct in MODULE.bazel.
build_test(
    name = "fetch-all-remotes",
    targets = [
        binary.format(platform)
        for binary in binaries
        for platform in execution_platforms
    ] + [
        "@libwit-bindgen-cabi-realloc//file",
        "@wasi-snapshot-preview1-reactor//file",
    ],
)

# Run the simple example command and save standard output to a file.
genrule(
    name = "command-output-actual",
    srcs = ["@example//rust:command"],
    outs = ["stdout.txt"],
    # Run without caching so it plays nice with GitHub Actions.
    # Note that wasmtime insists that the flags precede the component path.
    cmd = "$(location //:wasmtime) run --codegen cache=n $(location @example//rust:command) > \"$@\"",
    tools = ["//:wasmtime"],
)

# For sanity, check that the generated output from `:command-output-actual` is expected.
write_file(
    name = "command-output-expected",
    out = "expected.txt",
    content = ["I'm a command."],
)

diff_test(
    name = "verify-command-runs",
    file1 = ":command-output-actual",
    file2 = ":command-output-expected",
)
