load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("//:private.bzl", "execution_platforms")

# List of platform-dependent downloadable targets,
# where `{}` is a placeholder for the platform name.
platform_downloads = [
    "@wac-{}//file",
    "@wasm-tools-{}//:wasm-tools",
    "@wasi-sdk-{}//:clang",
    "@wasmtime-{}//:wasmtime",
    "@wit-bindgen-{}//:wit-bindgen",
    "@tinygo-{}//:tinygo",
]

# List of platform-independent downloadable targets.
universal_downloads = [
    "@libwit-bindgen-cabi-realloc//file",
    "@wasi-snapshot-preview1-reactor//file",
    "@wasi//:cli",
]

# Download all remote files for all platforms
# to make sure the checksums are correct in MODULE.bazel.
build_test(
    name = "fetch-all-remotes",
    targets = [
        download.format(platform)
        for download in platform_downloads
        for platform in execution_platforms
    ] + universal_downloads,
)

# Run the simple example command and save standard output to a file.
genrule(
    name = "command-output-actual",
    srcs = ["@example//rust:command"],
    outs = ["stdout.txt"],
    # Run without caching so it plays nice with GitHub Actions.
    # Note that wasmtime insists that the flags precede the component path.
    cmd = "$(location //:wasmtime) run --codegen cache=n $(location @example//rust:command) > \"$@\"",
    tools = ["//:wasmtime"],
)

# For sanity, check that the generated output from `:command-output-actual` is expected.
write_file(
    name = "command-output-expected",
    out = "expected.txt",
    content = ["I'm a command."],
)

diff_test(
    name = "verify-command-runs",
    file1 = ":command-output-actual",
    file2 = ":command-output-expected",
)
