load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@rules_cc//cc:defs.bzl", "cc_toolchain")
load(":private.bzl", "execution_platforms", "format_platform", "wasi_toolchain_config")

# Execution-platform-specific aliases for pre-built dependencies:
# Some custom rules need Bash, so Windows is out for now,
# but pre-built binaries might be available for other CPU's.

[selects.config_setting_group(
    name = "exe-" + platform,
    match_all = [
        "@platforms//cpu:" + platform.split("-")[0],
        "@platforms//os:" + platform.split("-")[1],
    ],
    visibility = ["//:__subpackages__"],
) for platform in execution_platforms]

native_binary(
    name = "wac",
    src = format_platform("@wac.{}//file"),
    visibility = ["//visibility:public"],
)

alias(
    name = "wasi-clang",
    actual = format_platform("@wasi-sdk.{}//:clang"),
    visibility = ["//visibility:public"],
)

alias(
    name = "wasm-opt",
    actual = format_platform("@binaryen.{}//:wasm-opt"),
    visibility = ["//visibility:public"],
)

alias(
    name = "wasm-tools",
    actual = format_platform("@wasm-tools.{}//:wasm-tools"),
    visibility = ["//visibility:public"],
)

alias(
    name = "wasmtime",
    actual = format_platform("@wasmtime.{}//:wasmtime"),
    visibility = ["//visibility:public"],
)

alias(
    name = "wit-bindgen",
    actual = format_platform("@wit-bindgen.{}//:wit-bindgen"),
    visibility = ["//visibility:public"],
)

# This file is also name-sensitive.
copy_file(
    name = "wasi-snapshot-preview1-reactor",
    src = "@wasi-snapshot-preview1-reactor//file",
    out = "wasi_snapshot_preview1.reactor.wasm",
    visibility = ["//wasm:__pkg__"],
)

# WASI C++ toolchain config:
# The *Rust* compiler claims this is necessary,
# but AFAICT we can just use garbage values for every argument
# and everything will still work fine.

filegroup(name = "empty")  # An empty file group.

wasi_toolchain_config(name = "wasm32-wasi-cc-toolchain-config")

cc_toolchain(
    name = "wasm32-wasi-cc-toolchain",
    all_files = ":empty",
    compiler_files = ":empty",
    dwp_files = ":empty",
    linker_files = ":empty",
    objcopy_files = ":empty",
    strip_files = ":empty",
    supports_param_files = False,
    toolchain_config = ":wasm32-wasi-cc-toolchain-config",
)

toolchain(
    name = "wasm32-wasi-toolchain",
    target_compatible_with = [
        "@platforms//cpu:wasm32",
        "@platforms//os:wasi",
    ],
    toolchain = ":wasm32-wasi-cc-toolchain",
    toolchain_type = "@bazel_tools//tools/cpp:toolchain_type",
)
